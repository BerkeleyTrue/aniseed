= Aniseed

== Sweeter Neovim with Fennel

____
Read my post, https://oli.me.uk/neovim-configuration-and-plugins-in-fennel-lisp/[Neovim configuration and plugins in Fennel (Lisp)], to get some more background on Aniseed.
____

Write https://fennel-lang.org/[Fennel] code and have https://neovim.io/[Neovim] compile and execute it via Lua. Configure and control your Neovim through the wonders of Lisp!

The project started as an experiment to see if I could replace my Neovim configuration in my https://github.com/Olical/dotfiles[dotfiles] as well as parts of https://github.com/Olical/conjure[Conjure] with Fennel.

If you're interested in Aniseed, you may find https://github.com/Olical/nvim-local-fennel[nvim-local-fennel] really useful too! It compiles and executes `.lnvim.fnl` files from your current working directory and above, allowing you to write machine or project local configuration in Fennel extremely easily. It also acts as a good example of how you can use Aniseed to develop your own plugins.

== Installation

Use your favourite plugin manager, I highly recommend https://github.com/junegunn/vim-plug[vim-plug] if you don't have one already.

Pin the plugin to a tag then subscribe to new releases through GitHub. I'd hate to accidentally break your entire configuration through a bad release or breaking change. I follow https://semver.org/[semver], any breaking changes will incur a major version bump, breaking changes and upgrade steps will be listed in the release notes.

[source,viml]
----
Plug 'Olical/aniseed', { 'tag': 'v2.0.0' }
----

== Compiling ahead of time

We can compile a single Fennel file to Lua through the `aniseed.compile` module.

[source,viml]
----
lua require("aniseed.compile").file("config.fnl", "config.lua")
----

To compile an entire directory of files matching a glob, such as your Neovim configuration or plugin source code, we can use the `glob` function in the same module.

[source,viml]
----
lua require("aniseed.compile").glob("**/*.fnl", "~/.config/nvim/fnl", "~/.config/nvim/lua")
----

This will compile all Fennel files found under the `fnl` directory into Lua and spit them out into the `lua` directory under the same directory structures as their source.

These functions will skip any source files which haven't changed since you last compiled them. If you wish to bypass this check you can pass an `opts` of `{force = true}` to either function as the last argument.

[source,viml]
----
lua require("aniseed.compile").file("config.fnl", "config.lua", {force = true})
----

== Evaluating with mappings

Ahead of time compiling covers most bases, but if you've ever written a Lisp you know how great REPL integration is. Aniseed provides some interactive evaluation, it's not quite https://github.com/Olical/conjure[Conjure] but it's still useful.

To set up the mappings you'll need to require `aniseed.mapping` and execute the `init` function contained within it. Then you can map your own keys to the `<Plug>` mappings it defined.

[source,clojure]
----
(local mapping (require :aniseed.mapping))
(mapping.init)

;; You can do this through regular Lua if you want.
;; lua require("aniseed.mapping").init()
----

|===
|Type |Name |Description

|Command
|`AniseedEval`
|Executes the Fennel passed to it, such as `:AniseedEval (+ 10 20)`. There's a function of the same name that this maps to.

|Command
|`AniseedEvalRange`
|Executes the Fennel in the given range, such as `:%AniseedEvalRange` or `:1,5AniseedEvalRange`. There's a function of the same name that this maps to.

|Command
|`AniseedEvalFile`
|Executes the Fennel contained in the file specified by the given path, won't AOT compile for future calls so it's really best suited to small one off things. There's a function of the same name that this maps to.

|Normal mapping
|`<Plug>(AniseedEval)`
|Should be followed by a motion to indicate what you want evaluated. Triggering this mapping with `10j` would evaluate the next 10 lines, for example.
If you have https://github.com/guns/vim-sexp[vim-sexp] you could use `af` to indicate your current form or `aF` for the outer most form.
You can even map to `<Plug>(AniseedEval)af` to create a mapping that evaluates your current form quickly.

|Normal mapping
|`<Plug>(AniseedEvalCurrentFile)`
|Evaluates the current file from disk.

|Visual mapping
|`<Plug>(AniseedEvalSelection)`
|Evaluates whatever you have visually selected.

|===

Here's the mappings I use written in Fennel, feel free to take what you want or just write your own in VimL!

[source,clojure]
----
;; We have access to https://github.com/norcalli/nvim.lua
;; Aniseed is really just combining cool things to be greater than the sum of it's parts.
(local nvim (require :aniseed.nvim))
(local mapping (require :aniseed.mapping))

;; Initialise all of Aniseed's mappings.
(mapping.init)

;; Helper function to make mapping into filetypes easier.
(fn ft-map [ft mode from to]
  "Map some keys (prefixed by <localleader>) to a command for a filetype."
  (nvim.ex.autocmd
    :FileType ft
    (.. mode :map) :<buffer>
    (.. :<localleader> from)
    to))

;; Helper for <Plug> mappings.
(fn plug [cmd]
  "Wraps the given command in <Plug>(...)"
  (.. "<Plug>(" cmd ")"))

;; Define some custom mappings to Aniseed commands.
(nvim.ex.augroup :aniseed)
(nvim.ex.autocmd_)
(ft-map :fennel :n :E (plug :AniseedEval))
(ft-map :fennel :n :ee (.. (plug :AniseedEval) :af))
(ft-map :fennel :n :er (.. (plug :AniseedEval) :aF))
(ft-map :fennel :n :ef (plug :AniseedEvalCurrentFile))
(ft-map :fennel :v :ee (plug :AniseedEvalSelection))
(ft-map :fennel :n :eb ":%AniseedEvalRange<cr>")
(ft-map :fennel :n :t ":AniseedRunAllTests<cr>")
(nvim.ex.augroup :END)
----

=== Example usage

Given a simple Fennel program and the mappings I described above, we could evaluate the following with `,ef` or `,eb` to evaluate the file from disk or the buffer. (my `+<localleader>+` is mapped to `+,+`, yours may differ!)

[source,clojure]
----
(fn add [a b]
  (+ a b))

(print (add 10 20))

{:add add}
----

Sadly we can't evaluate the `add` function and then the call to it like we would in https://clojure.org/[Clojure] with https://github.com/Olical/conjure[Conjure], that would require a concept of namespaces to give the evaluation some context.

The last line in the file defines the return value for the module. Modules, by convention, return a table of functions to expose, you could now call `add` from other Fennel or Lua (they're the same really) modules by requiring your file. In this example with could store it in `math.fnl` and require it with `(local math (require :math))`.

== Extras

=== Module reloading

Lua may not have namespaces, but it's module system will be enough for most purposes. One annoying thing about the system is that when you require a module it gets cached in a table. Subsequent requires return that cached value, not your new changes to the file.

If you would like your module to be updated as you work on it, simply add an `:aniseed/module` key to your module's return value. It should be set to the name you require the module with.

For example, if we had the `math` module from above which we wished to change and reload without restarting Neovim, we could add the following to it.

[source,clojure]
----
(local core (require :aniseed.core))

(fn add [a b]
  (+ a b))

;; Pretty print the output.
;; Just like Clojure's pr function.
;; There's a pr-str too!
(core.pr (add 10 20))

{:aniseed/module :math
 :add add}
----

Now when you evaluate the file the global `add` module will be updated with your changes as you see them in your buffer.

=== Clojure inspired utility functions

For now, the best documentation of the internal functions and API is the code. You'll find a bunch of Clojure like functions in `aniseed.core`, you can find that in `fnl/aniseed/core.fnl`.

There's also `aniseed.string`, `aniseed.fs` and `aniseed.nvim.util`. The last of which contains some helpful functions for creating bridge functions between VimL and Lua which is really useful for defining mappings, for example.

If you require more and you think the community would benefit from more ported Clojure functions, why not submit a pull request adding them.

=== Bundled libraries

There's a few dependencies which have some files copied into the `lua/aniseed` directory, they're extremely useful, Aniseed relies on them internally but you also have access to them.

 * `aniseed.fennel` - https://fennel-lang.org/[Fennel] itself, the entire compiler.
 * `aniseed.view` - the view function from https://fennel-lang.org/[Fennel], I'd recommend accessing it through `aniseed.core` and the `pr` or `pr-str` functions though.
 * `aniseed.nvim` - https://github.com/norcalli/nvim.lua[nvim.lua] is a collection of extremely helpful mappings to `vim.api.*`, I find it a lot easier to read than vanilla API access.

== Testing

Aniseed provides testing tools under `aniseed.test`, you can see how Aniseed executes it's own test suite (in CirclCI upon git push) in the `Makefile` under the `test` command. Check out the files in `+test/fnl/aniseed+` for further real world examples of Aniseed based testing.

The core of Aniseed's own suite is `test/fnl/aniseed/test-suite.fnl`.

[source,clojure]
----
(local nvim (require :aniseed.nvim))
(local test (require :aniseed.test))

(fn main []
  (nvim.ex.redir_ "> test/results.txt")

  (require :aniseed.core-test)
  (require :aniseed.compile-test)
  (require :aniseed.fs-test)
  (require :aniseed.string-test)

  (let [results (test.run-all)]
    (if (test.ok? results)
      (nvim.ex.q)
      (nvim.ex.cq))))

{:aniseed/module :aniseed.test-suite
 :main main}
----

It's ensuring all output is captured in a file, loading all of the test modules and then executing them with `+(test.run-all)+` (you can use `+(test.run :my.module)+` to run the tests for a specific module).

Your tests don't need to be in a specific directory like mine, they simply need to be in your module definition table under the `+:aniseed/tests+` key. Here's Aniseed's string test functions as an example. These could just as easily be in the `+aniseed.string+` module itself but I want to keep them out of the plugin's compiled output for your sake.

[source,clojure]
----
(local str (require :aniseed.string))

{:aniseed/module :aniseed.string-test
 :aniseed/tests
 {:join
  (fn [t]
    (t.= "foo, bar, baz" (str.join ", " ["foo" "bar" "baz"]))
    (t.= "foobarbaz" (str.join ["foo" "bar" "baz"])))}}
----

So your tests are named by keys in the `+aniseed/tests+` table, your test functions are called with a `t` table that contains the following assertion functions.

 * `t.ok?` checks if the result you give it is truthy.
 * `t.=` checks if the _expected_ and _actual_ (in that order) values are the same.
 * `t.pr=` is the same as `t.=` but will run the values through `core.pr-str`, this is handy for comparing tables.

All of these functions take an optional last argument that acts as a description on failure.

== Development

Aniseed is written in Fennel and compiled to Lua by the `Makefile` (please read it before executing it!), the compiled files are committed to the repository. Bear this in mind when considering a contribution!

You can use `make test` to run Aniseed's test suite with `aniseed.test`, it should be a good way to smoke test your changes despite the tests being quite sparse.

== Unlicenced

The following files are excluded from my license and ownership:

 * `lua/aniseed/fennel.lua`
 * `lua/aniseed/view.lua`
 * `lua/aniseed/nvim.lua`

These files come from https://fennel-lang.org/[Fennel] and https://github.com/norcalli/nvim.lua[nvim.lua], *I did not write them*, all other files are from me and unlicenced. The aforementioned files should be considered under their respective project licences. They are copied into this repo to allow the plugin to work with systems that don't support symlinks correctly.

Find the full http://unlicense.org/[unlicense] in the `UNLICENSE` file, but here's a snippet.

____
This is free and unencumbered software released into the public domain.

Anyone is free to copy, modify, publish, use, compile, sell, or distribute this software, either in source code form or as a compiled binary, for any purpose, commercial or non-commercial, and by any means.
____
