= Aniseed

Easily write configuration or plugins for https://neovim.io/[Neovim] in https://fennel-lang.org/[Fennel], a Lisp that compiles to Lua.

This is aimed at people who want to extend Neovim interactively with a Lisp as you would in some _other_ text editor. Code is evaluated as you work, it feels a lot like editing https://clojure.org/[Clojure] with https://github.com/Olical/conjure[Conjure].

It provides the tools you need to compile, run and test (even in CI!) Fennel within your editor. It includes a set of macros to make defining and depending on modules far more like Clojure than Lua. No more remembering to put values in a table at the bottom of the file.

[source,clojure]
----
(module my-module
  {;; Require other modules with aliases.
   require {core aniseed.core
            nvim aniseed.nvim}

   ;; Inline modules at compile time.
   include {some-dep my-other.dependency}})

(def- some-private-value 2)

(defn- some-private-fn [x]
  (* x some-private-value))

(defn a-public-fn [x]
  (-> x
      (some-private-fn)
      (core.inc)))

;; Prevent this value being overwritten on each eval.
(defonce- some-stateful-thing (some-dep.start-server!))

(comment
  (a-public-fn 10) ;; => 21
  (some-private-fn 10) ;; => 20
  (set nvim.g.hello "world!"))
----

Aniseed allows you to evaluate any form in this source code to build up your module dynamically with a constant REPL based flow. Once the buffer, file or forms have been evaluated you can even call the function from Lua and Vim Script.

[source,viml]
----
lua print(require("my-module")["a-public-fn"](10))
" Prints: 21

echo g:hello
" Prints: world!
----

https://github.com/Olical/nvim-local-fennel[nvim-local-fennel] is an example of what you can do with Aniseed, it allows you to write machine or directory specific configuration in Fennel. It's useful in it's own right but also a good reference for how to write a plugin.

== Installation

Use your favourite plugin manager, I recommend https://github.com/junegunn/vim-plug[vim-plug] if you don't have one already.

[source,viml]
----
" Depend on the latest version via tag.
Plug 'Olical/aniseed', { 'tag': 'v3.0.0' }

" For Fennel highlighting (based on Clojure).
Plug 'bakpakin/fennel.vim'

" Used by the evaluation mappings.
Plug 'guns/vim-sexp'

" Highly recommended if you're going to use vim-sexp.
Plug 'tpope/vim-sexp-mappings-for-regular-people'
----

The reliance on `vim-sexp` will be replaced by integration with https://github.com/Olical/conjure[Conjure] once I've finished rewriting it with Aniseed. Conjure, with the help of Aniseed, will become a generic suite for working with Lisps in Neovim.

== Mappings

You can use the following mappings inside Fennel buffers to run various chunks of code thanks to the `aniseed.mapping` module which is initialised for you on startup.

 * `<localleader>E[motion]` - eval the given motion of Fennel code.
 * `<localleader>ee` - eval the current form under the cursor.
 * `<localleader>er` - eval the outermost form under the cursor.
 * `<localleader>ef` - eval the file from disk.
 * `<localleader>eb` - eval the current buffer.
 * `<localleader>ee` (visual) - eval the visually selected code.
 * `<localleader>t` - run all loaded tests.

Feel free to have a peek in `fnl/aniseed/mapping.fnl` to see which commands are exposed, you'll be able to write your own custom mappings on top of those easily enough.

== Module macros

Lua and Fennel require you to use `(local ...)` and `(fn ...)` as well as a table as the last value in your file to define modules. This leads to inconsistent syntax and having to jump up and down in the buffer to keep your module's table in sync with what you want to expose.

This unstructured approach also makes it really hard (maybe impossible?) to have an interactive REPL driven work flow. We want to be able to say in a structured way "here's what my module is called, here's what it requires and here's what it defines".

Aniseed makes the following macros available to you which will allow you to write modules in a structured way using the evaluation mappings described previously. Let's get the most interesting and complex one out of the way first, the rest are just different ways of defining things.

=== `(module NAME [REQUIRES] [BASE])`

Define a module under the name given by the first argument, this should be the first form in your file. The module name should match the path to the source file with the slashes replaced with periods.

So `fnl/my-plugin/features/backflip.fnl` would start with `(module my-plugin.features.backflip)`. By using the period syntax we get to build paths that work on any OS, even if their path separator varies from what we're used to.

The requires are a table where the key is the function you want executed (`require` or `include`) and the values are tables of aliases and module names. Let's take our previous module example and require a utility module into it under an alias of `util`.

[source,clojure]
----
(module my-plugin.features.backflip
  {require {util my-plugin.util}})

(util.do-thing)
----

____
You're not limited to just `require` or `include`, any function name will do. If you write a special `require` called `require-py` that compiles Python to Lua and loads it, you can use that too!
____

The final argument can be used in rare scenarios where you want to build your module upon another Lua module which may not be related to Aniseed or Fennel in any way. Let's say you want to take a Lua library, wrap it with an Aniseed module and add some function to it.

[source,clojure]
----
(module my-plugin.math {} (include :some-awesome-lua-math-lib))

(defn inc [x]
  (+ x 1))
----

Now we have a module that inherits from a Lua module but extends it with an extra function. You probably won't need this, but it's handy when you do! Aniseed actually uses it internally to wrap the `nvim.lua` library.

=== The `(def...)` family

The `def...` macros help you define values or functions publicly or privately. Every `def` macro can be suffixed with a hyphen to keep it private, like `(def- ten 10)`.

 * `(def NAME VALUE)` - just like `local` or `var` but Aniseed is now aware of the value and will allow you to refer to it in subsequent evaluations of forms within that module.
 * `(defonce NAME VALUE)` - the same as `def` but will only define once. Subsequent evaluations of this form will do nothing. This is great for keeping process IDs from background jobs between evaluations.
 * `(defn NAME ARGS ...)` - translates to `(def NAME (fn ARGS ...))`, so it's just like the regular `(fn ...)` form really.
 * `(deftest NAME ...)` - explained in greater detail in the testing section.

== Standard library

Aniseed provides a fair few functions that it uses internally but are designed for use by plugin authors. The best way to discover these is to delve into `fnl/aniseed` and have a look through the source, anything that isn't self explanatory has a documentation string attached to it.

[source,clojure]
----
(module foo
  {require {core aniseed.core
            str aniseed.string}})

(defn- add [a b]
  (+ a b))

(->> [1 2 3]
     (core.map core.inc)
     (core.reduce add 0))
;; => 9

(str.join ", " [1 2 3])
;; => "1, 2, 3"
----

And here's some more useful modules you'll probably want to check out.

 * `aniseed.nvim` is actually https://github.com/norcalli/nvim.lua[nvim.lua], an extremely helpful library for controlling Neovim with a more expressive syntax.
 * `aniseed.eval` allows you to run more Fennel code through Aniseed.
 * `aniseed.compile` lets you compile strings and files of Fennel into Lua.

== Use case 1: Writing your dotfiles in Fennel

My Neovim https://github.com/Olical/dotfiles/tree/master/neovim/.config/nvim[dotfiles] are written in Fennel using Aniseed, all you need to do is add the following to your `init.vim`.

[source,viml]
----
lua require('aniseed.dotfiles')
----

Now try adding some Fennel, such as the example below, to `~/.config/nvim/fnl/dotfiles/init.fnl`. The next time you open Neovim you'll see `"Hello!"`.

[source,clojure]
----
(module dotfiles.init
  {require {core aniseed.core}})

(core.pr "Hello!")
----

This will write the compiled Lua into `~/.config/nvim/lua`, it's only recompiled if the Fennel changes. You'll want to add the `lua` directory to your `.gitignore` if your dotfiles are in git.

You can continue to add more files under `fnl/dotfiles` and require them using the `module` macro syntax.

== Use case 2: Compiling a plugin ahead of time

https://github.com/Olical/nvim-local-fennel[nvim-local-fennel] and the experimental https://github.com/Olical/conjure-sourcery[conjure-sourcery] should be good examples of Fennel based plugins (I hope so, I wrote them), here's how to create something new.

We'll start by fetching the `dep.sh` helper script which we'll use to clone and update Aniseed. It can be used for repos other than Aniseed, use and modify it as you see fit.

[source,bash]
----
mkdir -p scripts
curl https://raw.githubusercontent.com/Olical/aniseed/master/scripts/dep.sh -o scripts/dep.sh
chmod +x scripts/dep.sh
----

Now let's clone Aniseed into `deps/aniseed`, be sure to `.gitignore` the `deps` directory. I keep my `dep.sh` calls in my `Makefile` so I can run `make deps` to synchronise anything that's required.

[source,bash]
----
scripts/dep.sh Olical aniseed vX.Y.Z # insert latest version
----

Now let's add a small program to `fnl/my-plugin/init.fnl`.

[source,clojure]
----
(module my-plugin.init)

(print "Hello, World!")
----

We can compile the plugin using another helper script which will write into the `lua` directory, you should commit that output so that users of your plugin don't have to know it was ever written in Fennel in the first place.

[source,bash]
----
deps/aniseed/scripts/compile.sh
----

Users can call into your plugin however and whenever they choose or you can add some sort of startup hook into `plugin/my-plugin.vim`.

[source,viml]
----
lua require('my-plugin.init')

" Prints: Hello, World!
----

=== Using Aniseed at runtime in a plugin

I've only shown how to use Aniseed at compile time, there's one extra step involved when you want to use Aniseed in the runtime of your plugin. You can use `deps/aniseed/scripts/embed.sh` to copy Aniseed's Lua into your Lua directory under a unique prefix.

The code will be modified to add a prefix that keeps this version of Aniseed local to your plugin. This technique allows multiple plugins to carry their own copies of Aniseed with them without any conflicts or inconsistencies.

[source,bash]
----
deps/aniseed/scripts/embed.sh aniseed my-plugin
----

We can then refer to our Aniseed copy from `my-plugin.init`.

[source,clojure]
----
(module my-plugin.init
  {require {core my-plugin.aniseed.core}})

(core.pr {:msg "Hello, World!"})
----

== Testing

Aniseed tests itself with it's own testing tools, so you can see an example inside this very repository. We start by creating a module within our project such as `fnl/foo/math.fnl`.

[source,clojure]
----
(module foo.math)

(defn add [a b]
  (+ a b))
----

We can then create our test module in `test/fnl/foo/math-test.fnl`.

[source,clojure]
----
(module foo.math-test
  {require {math foo.math}})

;; The `t` assertion table is included automatically via the deftest macro.
(deftest add
  (t.= 10 (math.add 6 4) "it adds things together"))
----

All of the tests can then be executed using the helper script which will startup Neovim, execute the tests, capture the results and finally display them in stdout. It exits with an appropriate status code for use with CI systems (you'll need to ensure `nvim` is available inside your CI somehow).

[source,bash]
----
# You'll want to .gitginore /test/lua/ and /test/results.txt.
# Provide a SUFFIX or PREFIX env var to prepend or append code to the nvim command.
deps/aniseed/scripts/test.sh
----

The `t` assertion table that's defined for you contains a few assertion functions.

 * `(t.= expected result [description])` - check for an expected result.
 * `(t.pr= expected result [descrpition])` - the same but both values will be serialised to a string before comparison, this allows you to compare deeply nested tables.
 * `(t.ok? result [description])` - check for some truthy value.

== Makefile example

[source,make]
----
.PHONY: deps compile test

deps:
	scripts/dep.sh Olical aniseed vX.Y.Z # insert latest version

compile:
	# The rm optionally prevents hanging Lua files from when you delete Fennel files.
	# It's just a slightly cleaner build.
	rm -rf lua
	deps/aniseed/scripts/compile.sh
	deps/aniseed/scripts/embed.sh aniseed my-plugin

test:
	# This example ensures our tests run with a Fennel buffer open.
	SUFFIX="foo.fnl" deps/aniseed/scripts/test.sh
----

== Unlicenced

The following files are excluded from my license and ownership:

 * `lua/aniseed/deps/fennel.lua`
 * `lua/aniseed/deps/fennelview.lua`
 * `lua/aniseed/deps/nvim.lua`

These files come from https://fennel-lang.org/[Fennel] and https://github.com/norcalli/nvim.lua[nvim.lua], *I did not write them*, all other files are from me and unlicenced. The aforementioned files should be considered under their respective project licences. They are copied into this repo to allow the plugin to work with systems that don't support symlinks correctly.

Find the full http://unlicense.org/[unlicense] in the `UNLICENSE` file, but here's a snippet.

____
This is free and unencumbered software released into the public domain.

Anyone is free to copy, modify, publish, use, compile, sell, or distribute this software, either in source code form or as a compiled binary, for any purpose, commercial or non-commercial, and by any means.
____
